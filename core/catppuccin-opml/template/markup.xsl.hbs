<?xml version="1.0" encoding="ISO-8859-1"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:template match="/opml">
		<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
			<head>
				<meta name="viewport" content="width=device-width, initial-scale=1" />
				<meta name="referrer" content="unsafe-url" />
				<title>
					<xsl:value-of select="head/title" />
				</title>
				<style>{{style}}</style>
				<script>
					window.addEventListener('load', function() {
					var viewPara = document.getElementById("viewpara");
					viewPara.style.display = "block";
					var viewUrl = document.getElementById("viewUrl");
					viewUrl.value = window.location.href;
					});
				</script>
			</head>
			<body>
				<h1>
					<xsl:value-of select="head/title" />
				</h1>
				<p id="viewpara" style="display:none;">
					<form method="get" action="https://opml-viewer.fileformat.info/view.html"
						id="viewform">
						<input type="hidden" name="url" id="viewUrl" value="" />
						<!-- customize by adding more inputs with the viewer parameters --> Open in the <button>OPML
						Viewer</button> (requires JavaScript). </form>
				</p>
				<p>
					<time>
						<xsl:value-of select="head/dateCreated" />
					</time>
				</p>
				<xsl:apply-templates select="body/outline" />
			</body>
		</html>
	</xsl:template>
	<xsl:template match="outline" xmlns="http://www.w3.org/1999/xhtml">
		<xsl:choose>
			<xsl:when test="@type">
				<xsl:choose>
					<xsl:when test="@xmlUrl">
						<li>
							<div style="display: flex; gap: 8px; align-items: center;">
							<a href="{@xmlUrl}" style="display: flex; justify-content: center; align-items: center;">
								<img src="https://www.rss.style/favicon.svg"
									style="height:1rem;" alt="RSS/Atom Feed" />
							</a>
							<a href="{@htmlUrl}">
								<xsl:value-of select="@text" />
							</a>
							</div>
						</li>
					</xsl:when>
					<xsl:otherwise>
						<li>
							<a href="{@url}">
								<xsl:value-of select="@text" />
							</a>
						</li>
					</xsl:otherwise>
				</xsl:choose>
			</xsl:when>
			<xsl:otherwise>
				<details> <!-- adding an open="open" attribute will make everything open by default -->
					<summary>
						<xsl:value-of select="@text" />
					</summary>
					<ul>
						<xsl:apply-templates select="outline" />
					</ul>
				</details>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
</xsl:stylesheet>


